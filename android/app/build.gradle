plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        //noinspection GroovyAssignabilityCheck
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "0.0.1"
}

def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('key.properties')
if (keyPropertiesFile.exists()) {
    keyPropertiesFile.withReader('UTF-8') { reader ->
        //noinspection GroovyAssignabilityCheck
        keyProperties.load(reader)
    }
}
// Debug environment variables.
def signing_key_debug_path = keyProperties.getProperty('dev.SIGNING_KEY_DEBUG_PATH') ?: rootProject.SIGNING_KEY_DEBUG_PATH
def signing_key_debug_password = keyProperties.getProperty('dev.SIGNING_KEY_DEBUG_PASSWORD') ?: rootProject.SIGNING_KEY_DEBUG_PASSWORD
def signing_key_debug_key = keyProperties.getProperty('dev.SIGNING_KEY_DEBUG_KEY') ?: rootProject.SIGNING_KEY_DEBUG_KEY
def signing_key_debug_key_password = keyProperties.getProperty('dev.SIGNING_KEY_DEBUG_KEY_PASSWORD') ?: rootProject.SIGNING_KEY_DEBUG_KEY_PASSWORD
// Release environment variables.
def signing_key_release_path = keyProperties.getProperty('production.SIGNING_KEY_RELEASE_PATH') ?: rootProject.SIGNING_KEY_RELEASE_PATH
def signing_key_release_password = keyProperties.getProperty('production.SIGNING_KEY_RELEASE_PASSWORD') ?: rootProject.SIGNING_KEY_RELEASE_PASSWORD
def signing_key_release_key = keyProperties.getProperty('production.SIGNING_KEY_RELEASE_KEY') ?: rootProject.SIGNING_KEY_RELEASE_KEY
def signing_key_release_key_password = keyProperties.getProperty('production.SIGNING_KEY_RELEASE_KEY_PASSWORD') ?: rootProject.SIGNING_KEY_RELEASE_KEY_PASSWORD

android {
    namespace = rootProject.application_id
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = "21"
    }

    defaultConfig {
        applicationId rootProject.application_id

        minSdkVersion flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName

// To prevent com.getkeepsafe.relinker.MissingLibraryException.<init>
// F0.l - Could not find 'libflutter.so'. Looked for: [x86, armeabi-v7..
// https://stackoverflow.com/a/73556352/10636137
        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
    }

    signingConfigs {
        production {
            // FCI_BUILD_ID is exported by Codemagic
            //noinspection GroovyAssignabilityCheck
            if (System.getenv()["FCI_BUILD_ID"] != null) {
                //noinspection GroovyAssignabilityCheck
                storeFile file(System.getenv()["CM_KEYSTORE_PATH"])
                //noinspection GroovyAssignabilityCheck
                storePassword System.getenv()["CM_KEYSTORE_PASSWORD"]
                //noinspection GroovyAssignabilityCheck
                keyAlias System.getenv()["CM_KEY_ALIAS"]
                //noinspection GroovyAssignabilityCheck
                keyPassword System.getenv()["CM_KEY_PASSWORD"]
            } else {
                storeFile file(signing_key_release_path)
                storePassword signing_key_release_password
                keyAlias signing_key_release_key
                keyPassword signing_key_release_key_password
            }
        }
        dev {
            // FCI_BUILD_ID is exported by Codemagic
            //noinspection GroovyAssignabilityCheck
            if (System.getenv()["FCI_BUILD_ID"] != null) {
                //noinspection GroovyAssignabilityCheck
                storeFile file(System.getenv()["CM_KEYSTORE_PATH"])
                //noinspection GroovyAssignabilityCheck
                storePassword System.getenv()["CM_KEYSTORE_PASSWORD"]
                //noinspection GroovyAssignabilityCheck
                keyAlias System.getenv()["CM_KEY_ALIAS"]
                //noinspection GroovyAssignabilityCheck
                keyPassword System.getenv()["CM_KEY_PASSWORD"]
            } else {
                storeFile file(signing_key_debug_path)
                storePassword signing_key_debug_password
                keyAlias signing_key_debug_key
                keyPassword signing_key_debug_key_password
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig = signingConfigs.production
        }
        debug {
            signingConfig = signingConfigs.dev
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'com.google.firebase:firebase-crashlytics:20.0.3'
    implementation 'com.google.firebase:firebase-analytics:23.0.0'
    implementation("com.google.android.play:app-update-ktx:2.1.0")
}
